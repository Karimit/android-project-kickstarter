apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'io.fabric'

def keystorePropertiesFile = rootProject.file("keystore.properties")
// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()
// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 28
    buildToolsVersion "29.0.1"
    defaultConfig {
        applicationId "com.example.app"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        multiDexEnabled = true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true

            // R8 uses ProGuard rules files
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [enableCrashReporting: "true"]
            signingConfig signingConfigs.release
        }

        debug {
            manifestPlaceholders = [enableCrashReporting: "false"]
            pseudoLocalesEnabled = true // https://developer.android.com/guide/topics/resources/pseudolocales
            applicationIdSuffix ".debug"
        }
    }
    compileOptions {
        targetCompatibility = 1.8
        sourceCompatibility = 1.8
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // AndroidXCore, Appcompat, Multidex
    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.multidex:multidex:$multidex_version"

    // Lifecycle
    // ViewModel and LiveData and test helpers for LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    testImplementation "androidx.arch.core:core-testing:$lifecycle_version"

    // Fragment... and Testing Fragments in Isolation
    implementation "androidx.fragment:fragment-ktx:$fragment_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // Savedstate
    implementation "androidx.savedstate:savedstate:$savedstate_version"

    // Vector
    implementation "androidx.vectordrawable:vectordrawable:$vectordrawable_version"

    // UI
    // Material Components
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.constraintlayout:constraintlayout:$coordinator_layout_version"
    implementation "androidx.recyclerview:recyclerview:$recycler_version"

    // Koin
    // Koin AndroidX Scope, vm, fragment, and experimental features
    implementation "org.koin:koin-androidx-scope:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    implementation "org.koin:koin-androidx-ext:$koin_version"

    // Timber
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Firebase Crashlytics
    implementation "com.crashlytics.sdk.android:crashlytics:$firebase_crashlytics_version"

    // ThreeTenABP. Java 8 Time backport
    implementation "com.jakewharton.threetenabp:threetenabp:$threetenabp_version"

    // Leakcanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"

    // Stetho
    implementation "com.facebook.stetho:stetho:$stetho_version"
    implementation "com.facebook.stetho:stetho-okhttp3:$stetho_version"

    // Test
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test.ext:junit:$androidx_test_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"

    implementation project(':domain')
}

apply plugin: 'com.google.gms.google-services'